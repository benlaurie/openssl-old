$! INSTALL.COM -- Installs the files in a given directory tree
$!
$! Author: Richard Levitte <richard@levitte.org>
$! Time of creation: 23-MAY-1998 19:22
$!
$! P1	root of the directory tree
$! P2	prefix for certain file (this is really for Compaq, who need to
$!	use the SSL$ prefix)
$!
$	IF P1 .EQS. ""
$	THEN
$	    WRITE SYS$OUTPUT "First argument missing."
$	    WRITE SYS$OUTPUT "Should be the directory where you want things installed."
$	    EXIT
$	ENDIF
$
$	IF P2 .NES. ""
$	THEN
$	    FILE_PREFIX = P2
$	    LOG_PREFIX = P2
$	ELSE
$	    FILE_PREFIX = "OPENSSL_"
$	    LOG_PREFIX = "SSL"
$	ENDIF
$
$	ROOT = F$PARSE(P1,"[]A.;0",,,"SYNTAX_ONLY,NO_CONCEAL") - "A.;0"
$	ROOT_DEV = F$PARSE(ROOT,,,"DEVICE","SYNTAX_ONLY")
$	ROOT_DIR = F$PARSE(ROOT,,,"DIRECTORY","SYNTAX_ONLY") -
		   - "[000000." - "][" - "[" - "]"
$	ROOT = ROOT_DEV + "[" + ROOT_DIR
$
$	DEFINE/NOLOG WRK_SSLROOT 'ROOT'.] /TRANS=CONC
$	DEFINE/NOLOG WRK_SSLVLIB WRK_SSLROOT:[VAX_LIB]
$	DEFINE/NOLOG WRK_SSLALIB WRK_SSLROOT:[ALPHA_LIB]
$	DEFINE/NOLOG WRK_SSLINCLUDE WRK_SSLROOT:[INCLUDE]
$	DEFINE/NOLOG WRK_SSLVEXE WRK_SSLROOT:[VAX_EXE]
$	DEFINE/NOLOG WRK_SSLAEXE WRK_SSLROOT:[ALPHA_EXE]
$	DEFINE/NOLOG WRK_SSLCERTS WRK_SSLROOT:[CERTS]
$	DEFINE/NOLOG WRK_SSLPRIVATE WRK_SSLROOT:[PRIVATE]
$	DEFINE/NOLOG WRK_SSLCOM WRK_SSLROOT:[COM]
$
$	IF F$PARSE("WRK_SSLROOT:[000000]") .EQS. "" THEN -
	   CREATE/DIR/LOG WRK_SSLROOT:[000000]
$	IF F$PARSE("WRK_SSLINCLUDE:") .EQS. "" THEN -
	   CREATE/DIR/LOG WRK_SSLINCLUDE:
$	IF F$PARSE("WRK_SSLROOT:[VMS]") .EQS. "" THEN -
	   CREATE/DIR/LOG WRK_SSLROOT:[VMS]
$	IF F$PARSE("WRK_SSLCOM:") .EQS. "" THEN -
	   CREATE/DIR/LOG WRK_SSLROOT:[COM]
$
$	IF F$SEARCH("WRK_SSLINCLUDE:vms_idhacks.h") .NES. "" THEN -
	   DELETE WRK_SSLINCLUDE:vms_idhacks.h;*
$
$	OPEN/WRITE SF WRK_SSLCOM:'FILE_PREFIX'STARTUP.COM
$	WRITE SYS$OUTPUT "%OPEN-I-CREATED,  ",F$SEARCH("WRK_SSLCOM:''FILE_PREFIX'STARTUP.COM")," created."
$	WRITE SF "$! Startup file for OpenSSL"
$	WRITE SF "$!"
$	WRITE SF "$! Do not edit this file, as it will be regenerated during next installation."
$	WRITE SF "$! Instead, add or change ''LOG_PREFIX'COM:'''FILE_PREFIX'SYSTARTUP.COM"
$	WRITE SF "$!"
$	WRITE SF "$!"
$	WRITE SF "$	ARCH = ""VAX"""
$	WRITE SF "$	IF F$GETSYI(""CPU"") .GE. 128 THEN ARCH = ""ALPHA"""
$	WRITE SF "$!"
$	WRITE SF "$ IF F$SEARCH(""SYS$STARTUP:''FILE_PREFIX'DEFINE_ROOT.COM"") .NES."""" THEN -"
$	WRITE SF "$	@SYS$STARTUP:''FILE_PREFIX'DEFINE_ROOT.COM"
$	WRITE SF "$!"
$	WRITE SF "$!"
$	WRITE SF "$	DEFINE/NOLOG/SYSTEM/EXEC	''LOG_PREFIX'COM		''LOG_PREFIX'ROOT:[COM]"
$	WRITE SF "$	DEFINE/NOLOG/SYSTEM/EXEC	''LOG_PREFIX'EXE		''LOG_PREFIX'ROOT:['ARCH'_EXE]"
$	WRITE SF "$	DEFINE/NOLOG/SYSTEM/EXEC	''LOG_PREFIX'INCLUDE	''LOG_PREFIX'ROOT:[INCLUDE]"
$	WRITE SF "$	DEFINE/NOLOG/SYSTEM/EXEC	''LOG_PREFIX'CERTS	''LOG_PREFIX'ROOT:[CERTS]"
$	WRITE SF "$	DEFINE/NOLOG/SYSTEM/EXEC	''LOG_PREFIX'PRIVATE	''LOG_PREFIX'ROOT:[PRIVATE]"
$	WRITE SF "$	DEFINE/NOLOG/SYSTEM/EXEC	''LOG_PREFIX'KEYS		''LOG_PREFIX'ROOT:[PRIVATE]"
$	WRITE SF "$"
$	WRITE SF "$!	This is program can include <openssl/{foo}.h>"
$	WRITE SF "$	DEFINE/NOLOG/SYSTEM/EXEC	OPENSSL		''LOG_PREFIX'INCLUDE:"
$	WRITE SF "$!"
$	WRITE SF "$	IF F$SEARCH(""''LOG_PREFIX'COM:''FILE_PREFIX'SYSTARTUP.COM"") .NES."""" THEN -"
$	WRITE SF "	   @''LOG_PREFIX'COM:''FILE_PREFIX'SYSTARTUP.COM"
$	WRITE SF "$"
$	WRITE SF "$	EXIT"
$	CLOSE SF
$	SET FILE/PROT=WORLD:RE WRK_SSLCOM:'FILE_PREFIX'STARTUP.COM
$!
$	OPEN/WRITE SF WRK_SSLCOM:'FILE_PREFIX'SHUTDOWN.COM
$	WRITE SYS$OUTPUT "%OPEN-I-CREATED,  ",F$SEARCH("WRK_SSLCOM:''FILE_PREFIX'SHUTDOWN.COM")," created."
$	WRITE SF "$! Shutdown file for OpenSSL"
$	WRITE SF "$!"
$	WRITE SF "$! Do not edit this file, as it will be regenerated during next installation."
$	WRITE SF "$!"
$	WRITE SF "$	ARCH = ""VAX"""
$	WRITE SF "$	IF F$GETSYI(""CPU"") .GE. 128 THEN ARCH = ""ALPHA"""
$	WRITE SF "$!"
$	WRITE SF "$ IF F$SEARCH(""''LOG_PREFIX'COM:''FILE_PREFIX'SYSHUTDOWN.COM"") .NES."""" THEN -"
$	WRITE SF "	@''LOG_PREFIX'COM:''FILE_PREFIX'SYSHUTDOWN.COM"
$	WRITE SF "$!"
$	WRITE SF "$ IF F$TRNLNM(""''LOG_PREFIX'CERTS"") .NES."""" THEN -"
$	WRITE SF "	DEASSIGN/SYSTEM/EXEC	''LOG_PREFIX'CERTS"
$!
$	WRITE SF "$ IF F$TRNLNM(""''LOG_PREFIX'COM"") .NES."""" THEN -"
$	WRITE SF "	DEASSIGN/SYSTEM/EXEC	''LOG_PREFIX'COM"
$!
$	WRITE SF "$ IF F$TRNLNM(""''LOG_PREFIX'EXE"") .NES."""" THEN -"
$	WRITE SF "	DEASSIGN/SYSTEM/EXEC	''LOG_PREFIX'EXE"
$!
$	WRITE SF "$ IF F$TRNLNM(""''LOG_PREFIX'INCLUDE"") .NES."""" THEN -"
$	WRITE SF "	DEASSIGN/SYSTEM/EXEC	''LOG_PREFIX'INCLUDE"
$!
$	WRITE SF "$ IF F$TRNLNM(""''LOG_PREFIX'CERTS"") .NES."""" THEN -"
$	WRITE SF "	DEASSIGN/SYSTEM/EXEC	''LOG_PREFIX'CERTS"
$!
$	WRITE SF "$ IF F$TRNLNM(""''LOG_PREFIX'PRIVATE"") .NES."""" THEN -"
$	WRITE SF "	DEASSIGN/SYSTEM/EXEC	''LOG_PREFIX'PRIVATE"
$!
$	WRITE SF "$ IF F$TRNLNM(""''LOG_PREFIX'KEYS"") .NES."""" THEN -"
$	WRITE SF "	DEASSIGN/SYSTEM/EXEC	''LOG_PREFIX'KEYS"
$!
$	WRITE SF "$!"
$	WRITE SF "$ IF F$TRNLNM(""OPENSSL"") .NES."""" THEN -"
$	WRITE SF "	DEASSIGN/SYSTEM/EXEC	OPENSSL"
$	WRITE SF "$!"
$	WRITE SF "$ IF F$TRNLNM(""''LOG_PREFIX'ROOT"") .NES."""" THEN -"
$	WRITE SF "	DEASSIGN/SYSTEM/EXEC ''LOG_PREFIX'ROOT"
$	WRITE SF "$!"
$	WRITE SF "$	EXIT"
$	CLOSE SF
$	SET FILE/PROT=WORLD:RE WRK_SSLCOM:'FILE_PREFIX'SHUTDOWN.COM
$!
$	OPEN/WRITE SF WRK_SSLCOM:'FILE_PREFIX'UTILS.COM
$	WRITE SYS$OUTPUT "%OPEN-I-CREATED,  ",F$SEARCH("WRK_SSLCOM:''FILE_PREFIX'UTILS.COM")," created."
$	WRITE SF "$!"
$	WRITE SF "$!  APPS.COM"
$	WRITE SF "$!  Written By:  Robert Byer"
$	WRITE SF "$!               Vice-President"
$	WRITE SF "$!               A-Com Computing, Inc."
$	WRITE SF "$!               byer@mail.all-net.net"
$	WRITE SF "$!"
$	WRITE SF "$!"
$	WRITE SF "$! Slightly modified by Richard Levitte <richard@levitte.org>"
$	WRITE SF "$!"
$	WRITE SF "$ IF P1 .NES. "" THEN GOTO 'P1"
$	WRITE SF "$!"
$	WRITE SF "$DEFINE:"
$	WRITE SF "$!"
$	WRITE SF "$ OPENSSL  :== $''LOG_PREFIX'EXE:OPENSSL"
$	WRITE SF "$ VERIFY   :== $''LOG_PREFIX'EXE:OPENSSL VERIFY"
$	WRITE SF "$ ASN1PARSE:== $''LOG_PREFIX'EXE:OPENSSL ASN1PARS"
$	WRITE SF "$ REQ      :== $''LOG_PREFIX'EXE:OPENSSL REQ"
$	WRITE SF "$ DGST     :== $''LOG_PREFIX'EXE:OPENSSL DGST"
$	WRITE SF "$ DH       :== $''LOG_PREFIX'EXE:OPENSSL DH"
$	WRITE SF "$ ENC      :== $''LOG_PREFIX'EXE:OPENSSL ENC"
$	WRITE SF "$ GENDH    :== $''LOG_PREFIX'EXE:OPENSSL GENDH"
$	WRITE SF "$ ERRSTR   :== $''LOG_PREFIX'EXE:OPENSSL ERRSTR"
$	WRITE SF "$ CA       :== $''LOG_PREFIX'EXE:OPENSSL CA"
$	WRITE SF "$ CRL      :== $''LOG_PREFIX'EXE:OPENSSL CRL"
$	WRITE SF "$ RSA      :== $''LOG_PREFIX'EXE:OPENSSL RSA"
$	WRITE SF "$ DSA      :== $''LOG_PREFIX'EXE:OPENSSL DSA"
$	WRITE SF "$ DSAPARAM :== $''LOG_PREFIX'EXE:OPENSSL DSAPARAM"
$	WRITE SF "$ X509     :== $''LOG_PREFIX'EXE:OPENSSL X509"
$	WRITE SF "$ GENRSA   :== $''LOG_PREFIX'EXE:OPENSSL GENRSA"
$	WRITE SF "$ GENDSA   :== $''LOG_PREFIX'EXE:OPENSSL GENDSA"
$	WRITE SF "$ S_SERVER :== $''LOG_PREFIX'EXE:OPENSSL S_SERVER"
$	WRITE SF "$ S_CLIENT :== $''LOG_PREFIX'EXE:OPENSSL S_CLIENT"
$	WRITE SF "$ SPEED    :== $''LOG_PREFIX'EXE:OPENSSL SPEED"
$	WRITE SF "$ S_TIME   :== $''LOG_PREFIX'EXE:OPENSSL S_TIME"
$	WRITE SF "$ VERSION  :== $''LOG_PREFIX'EXE:OPENSSL VERSION"
$	WRITE SF "$ PKCS7    :== $''LOG_PREFIX'EXE:OPENSSL PKCS7"
$	WRITE SF "$ CRL2PKCS7:== $''LOG_PREFIX'EXE:OPENSSL CRL2P7"
$	WRITE SF "$ SESS_ID  :== $''LOG_PREFIX'EXE:OPENSSL SESS_ID"
$	WRITE SF "$ CIPHERS  :== $''LOG_PREFIX'EXE:OPENSSL CIPHERS"
$	WRITE SF "$ NSEQ     :== $''LOG_PREFIX'EXE:OPENSSL NSEQ"
$	WRITE SF "$ PKCS12   :== $''LOG_PREFIX'EXE:OPENSSL PKCS12"
$	WRITE SF "$!"
$	WRITE SF "$EXIT"
$	WRITE SF "$!"
$	WRITE SF "$REMOVE:"
$	WRITE SF "$ DELETE/SYMBOL/GLOBAL OPENSSL"
$	WRITE SF "$ DELETE/SYMBOL/GLOBAL VERIFY"
$	WRITE SF "$ DELETE/SYMBOL/GLOBAL ASN1PARSE"
$	WRITE SF "$ DELETE/SYMBOL/GLOBAL REQ"
$	WRITE SF "$ DELETE/SYMBOL/GLOBAL DGST"
$	WRITE SF "$ DELETE/SYMBOL/GLOBAL DH"
$	WRITE SF "$ DELETE/SYMBOL/GLOBAL ENC"
$	WRITE SF "$ DELETE/SYMBOL/GLOBAL GENDH"
$	WRITE SF "$ DELETE/SYMBOL/GLOBAL ERRSTR"
$	WRITE SF "$ DELETE/SYMBOL/GLOBAL CA"
$	WRITE SF "$ DELETE/SYMBOL/GLOBAL CRL"
$	WRITE SF "$ DELETE/SYMBOL/GLOBAL RSA"
$	WRITE SF "$ DELETE/SYMBOL/GLOBAL DSA"
$	WRITE SF "$ DELETE/SYMBOL/GLOBAL DSAPARAM"
$	WRITE SF "$ DELETE/SYMBOL/GLOBAL X509"
$	WRITE SF "$ DELETE/SYMBOL/GLOBAL GENRSA"
$	WRITE SF "$ DELETE/SYMBOL/GLOBAL GENDSA"
$	WRITE SF "$ DELETE/SYMBOL/GLOBAL S_SERVER"
$	WRITE SF "$ DELETE/SYMBOL/GLOBAL S_CLIENT"
$	WRITE SF "$ DELETE/SYMBOL/GLOBAL SPEED"
$	WRITE SF "$ DELETE/SYMBOL/GLOBAL S_TIME"
$	WRITE SF "$ DELETE/SYMBOL/GLOBAL VERSION"
$	WRITE SF "$ DELETE/SYMBOL/GLOBAL PKCS7"
$	WRITE SF "$ DELETE/SYMBOL/GLOBAL CRL2PKCS7"
$	WRITE SF "$ DELETE/SYMBOL/GLOBAL SESS_ID"
$	WRITE SF "$ DELETE/SYMBOL/GLOBAL CIPHERS"
$	WRITE SF "$ DELETE/SYMBOL/GLOBAL NSEQ"
$	WRITE SF "$ DELETE/SYMBOL/GLOBAL PKCS12"
$	WRITE SF "$!"
$	WRITE SF "$EXIT"
$	CLOSE SF
$	SET FILE/PROT=WORLD:RE WRK_SSLCOM:'FILE_PREFIX'UTILS.COM
$!
$	CERT_DIR := [.CERT_TOOL]
$	CERT_FILES := SSL$AUTH_CERT.COM,SSL$AUTO_CERT.COM,SSL$CERT_TOOL.COM, -
		      SSL$CONF_UTIL.COM,SSL$DRAW_BOX.COM,SSL$EXIT_CMD.TPU, -
		      SSL$FILL_BOX.COM,SSL$HASH_CERT.COM,SSL$HOSTADDR.EXE, -
		      SSL$HOSTNAME.EXE,SSL$INIT_ENV.COM,SSL$INIT_TERM.COM, -
		      SSL$PICK_FILE.COM,SSL$RQST_CERT.COM,SSL$SELF_CERT.COM, -
		      SSL$SHOW_FILE.COM,SSL$SIGN_CERT.COM,SSL$VIEW_CERT.COM, -
		      SSL$REM_ENV.COM
$!
$	I = 0
$ LOOP:
$       CF = F$EDIT(F$ELEMENT(I, ",", CERT_FILES),"TRIM")
$       I = I + 1
$       IF CF .EQS. "," THEN GOTO LOOP_END
$       SET NOON
$       IF F$SEARCH(CERT_DIR+CF) .NES. ""
$       THEN
$	  IF CF - ".EXE" .NES. CF
$	  THEN
$           COPY 'CERT_DIR''CF' WRK_SSLEXE:*.*/log
$           SET FILE/PROT=W:RE WRK_SSLEXE:'CF'
$	  ELSE
$           COPY 'CERT_DIR''CF' WRK_SSLCOM:*.*/log
$           SET FILE/PROT=W:RE WRK_SSLCOM:'CF'
$	  ENDIF
$       ENDIF
$       SET ON
$       GOTO LOOP
$ LOOP_END:
$
$	EXIT
