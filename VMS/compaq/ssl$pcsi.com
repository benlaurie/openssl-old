$!
$! SSL$PCSI.COM - This file is executed during the PCSI
$!		  installation.  It's main task is to 
$!		  translate the PCSI$DESTINATION logical
$!		  and use the equivalence value as the 
$!		  value for SSL$ROOT.
$!
$! P1 = "INSTALL" or "REMOVE", depending whether this procedure
$!      is being called during a PRODUCT INSTALL/REMOVE.
$!
$ if p1 .eqs. "INSTALL"
$ then
$    ! Give the person doing the installation a clue where the
$    ! SSL files has been placed. He or she will need
$    ! to know this in order to configure the new installation and
$    ! for editing SYS$MANAGER:SYSTARTUP_VMS.COM
$
$    ! Rely upon PCSI to set PCSI$DESTINATION up with choice of
$    ! [] or <> in use on this site and to have translated the device NO_CONCEAL.
$
$    raw_dest = f$trnlnm("PCSI$DESTINATION")
$    raw_dev  = f$parse("''raw_dest'",,, "DEVICE", "NO_CONCEAL")
$    raw_dev  = f$edit("''raw_dev'", "UPCASE")
$    raw_dir  = f$parse("''raw_dest'",,, "DIRECTORY", "NO_CONCEAL")
$    raw_dir  = f$edit("''raw_dir'", "UPCASE")
$
$    sys_common = f$trnlnm("SYS$COMMON")
$    sys_common_dev  = f$parse("''sys_common'",,, "DEVICE", "NO_CONCEAL")
$    sys_common_dir =  f$parse("''sys_common'",,, "DIRECTORY", "NO_CONCEAL")
$
$    if "''raw_dir'" .eqs. "''sys_common_dir'" -
        .and.  "''raw_dev'"  .eqs. "''sys_common_dev'"
$    then
$       equiv = sys_common - "]" + "SSL.]"
$    else
$       len = f$length("''raw_dest'")
$       end_ch  = f$extract(len - 1, 1, "''raw_dest'")
$       dest_prefix = "''raw_dest'" - "''end_ch'"
$       dest_prefix = "''dest_prefix'" - "000000."
$
$       equiv = "''dest_prefix'SSL.''end_ch'"
$    endif
$!
$    open/write root_file sys$startup:ssl$define_root.com
$    write root_file "$!"
$    write root_file "$! Do not edit this file."
$    write root_file "$! This file is created by SSL$PCSI.COM.  SSL$PCSI.COM should"
$    write root_file "$! be changed to have modifications to this file take effect."
$    write root_file "$!"
$    write root_file "$ DEFINE/SYSTEM/EXECUTIVE_MODE/TRANSLATION=CONCEALED SSL$ROOT ''equiv'"
$    write root_file "$!"
$    close root_file
$!
$ endif
$!
$ if p1 .eqs. "REMOVE"
$ then
$    if f$trnlnm("SSL$ROOT") .nes. ""
$    then
$       deassign/system/executive_mode ssl$root
$    endif
$!
$    if f$search("sys$startup:ssl$define_root.com") .nes. ""
$    then
$       delete sys$startup:ssl$define_root.com;*
$    endif
$!
$ endif
$!
$ exit
