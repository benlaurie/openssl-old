$! INSTALL.COM -- Installs the files in a given directory tree
$!
$! Author: Richard Levitte <richard@levitte.org>
$! Time of creation: 23-MAY-1998 19:22
$!
$! P1	root of the directory tree
$!
$	IF P1 .EQS. ""
$	THEN
$	    WRITE SYS$OUTPUT "First argument missing."
$	    WRITE SYS$OUTPUT "Should be the directory where you want things installed."
$	    EXIT
$	ENDIF
$
$	ROOT = F$PARSE(P1,"[]A.;0",,,"SYNTAX_ONLY,NO_CONCEAL") - "A.;0"
$	ROOT_DEV = F$PARSE(ROOT,,,"DEVICE","SYNTAX_ONLY")
$	ROOT_DIR = F$PARSE(ROOT,,,"DIRECTORY","SYNTAX_ONLY") -
		   - "[000000." - "][" - "[" - "]"
$	ROOT = ROOT_DEV + "[" + ROOT_DIR
$
$	DEFINE/NOLOG WRK_SSLROOT 'ROOT'.] /TRANS=CONC
$	DEFINE/NOLOG WRK_SSLVLIB WRK_SSLROOT:[VAX_LIB]
$	DEFINE/NOLOG WRK_SSLALIB WRK_SSLROOT:[ALPHA_LIB]
$	DEFINE/NOLOG WRK_SSLINCLUDE WRK_SSLROOT:[INCLUDE]
$	DEFINE/NOLOG WRK_SSLVEXE WRK_SSLROOT:[VAX_EXE]
$	DEFINE/NOLOG WRK_SSLAEXE WRK_SSLROOT:[ALPHA_EXE]
$	DEFINE/NOLOG WRK_SSLCERTS WRK_SSLROOT:[CERTS]
$	DEFINE/NOLOG WRK_SSLPRIVATE WRK_SSLROOT:[PRIVATE]
$	DEFINE/NOLOG WRK_SSLCOM WRK_SSLROOT:[COM]
$
$	IF F$PARSE("WRK_SSLROOT:[000000]") .EQS. "" THEN -
	   CREATE/DIR/LOG WRK_SSLROOT:[000000]
$	IF F$PARSE("WRK_SSLINCLUDE:") .EQS. "" THEN -
	   CREATE/DIR/LOG WRK_SSLINCLUDE:
$!	IF F$PARSE("WRK_SSLROOT:[VMS]") .EQS. "" THEN -
$!	   CREATE/DIR/LOG WRK_SSLROOT:[VMS]
$	IF F$PARSE("WRK_SSLCOM:") .EQS. "" THEN -
	   CREATE/DIR/LOG WRK_SSLROOT:[COM]
$
$	IF F$SEARCH("WRK_SSLINCLUDE:vms_idhacks.h") .NES. "" THEN -
	   DELETE WRK_SSLINCLUDE:vms_idhacks.h;*
$
$	OPEN/WRITE SF WRK_SSLCOM:SSL$STARTUP.COM
$	WRITE SYS$OUTPUT "%OPEN-I-CREATED,  ",F$SEARCH("WRK_SSLCOM:SSL$STARTUP.COM")," created."
$	WRITE SF "$! Startup file for SSL 0.9.2-RL 15-Mar-1999"
$	WRITE SF "$!"
$	WRITE SF "$! Do not edit this file, as it will be regenerated during next installation."
$	WRITE SF "$! Instead, add or change SSL$COM:SSL$SYSTARTUP.COM"
$	WRITE SF "$!"
$	WRITE SF "$!"
$	WRITE SF "$	ARCH = ""VAX"""
$	WRITE SF "$	IF F$GETSYI(""CPU"") .GE. 128 THEN ARCH = ""ALPHA"""
$	WRITE SF "$!"
$	WRITE SF "$ IF F$SEARCH(""SYS$STARTUP:SSL$DEFINE_ROOT.COM"") .NES."""" THEN -"
$	WRITE SF "$	@SYS$STARTUP:SSL$DEFINE_ROOT.COM"
$	WRITE SF "$!"
$	WRITE SF "$!"
$	WRITE SF "$	DEFINE/NOLOG/SYSTEM/EXEC	SSL$CERTS	SSL$ROOT:[CERTS]"
$	WRITE SF "$	DEFINE/NOLOG/SYSTEM/EXEC	SSL$COM		SSL$ROOT:[COM]"
$	WRITE SF "$	DEFINE/NOLOG/SYSTEM/EXEC	SSL$EXE		SSL$ROOT:['ARCH'_EXE]"
$	WRITE SF "$	DEFINE/NOLOG/SYSTEM/EXEC	SSL$INCLUDE	SSL$ROOT:[INCLUDE]"
$	WRITE SF "$	DEFINE/NOLOG/SYSTEM/EXEC	SSL$KEY		SSL$ROOT:[CERTS]"
$	WRITE SF "$	DEFINE/NOLOG/SYSTEM/EXEC	SSL$PRIVATE	SSL$ROOT:[PRIVATE]"
$	WRITE SF "$"
$	WRITE SF "$!	This is program can include <openssl/{foo}.h>"
$	WRITE SF "$	DEFINE/NOLOG/SYSTEM/EXEC	OPENSSL		SSL$INCLUDE:"
$	WRITE SF "$!"
$	WRITE SF "$	IF F$SEARCH(""SSL$COM:SSL$SYSTARTUP.COM"") .NES."""" THEN -"
$	WRITE SF "	   @SSL$COM:SSL$SYSTARTUP.COM"
$	WRITE SF "$"
$	WRITE SF "$	EXIT"
$	CLOSE SF
$	SET FILE/PROT=WORLD:RE WRK_SSLCOM:SSL$STARTUP.COM
$!
$	OPEN/WRITE SF WRK_SSLCOM:SSL$SHUTDOWN.COM
$	WRITE SYS$OUTPUT "%OPEN-I-CREATED,  ",F$SEARCH("WRK_SSLCOM:SSL$SHUTDOWN.COM")," created."
$	WRITE SF "$! Shutdown file for SSL"
$	WRITE SF "$!"
$	WRITE SF "$! Do not edit this file, as it will be regenerated during next installation."
$	WRITE SF "$!"
$	WRITE SF "$	ARCH = ""VAX"""
$	WRITE SF "$	IF F$GETSYI(""CPU"") .GE. 128 THEN ARCH = ""ALPHA"""
$	WRITE SF "$!"
$	WRITE SF "$ IF F$SEARCH(""SSL$COM:SSL$SYSHUTDOWN.COM"") .NES."""" THEN -"
$	WRITE SF "	@SSL$COM:SSL$SYSHUTDOWN.COM"
$	WRITE SF "$!"
$	WRITE SF "$ IF F$TRNLNM(""SSL$CERTS"") .NES."""" THEN -"
$	WRITE SF "	DEASSIGN/SYSTEM/EXEC	SSL$CERTS"
$!
$	WRITE SF "$ IF F$TRNLNM(""SSL$COM"") .NES."""" THEN -"
$	WRITE SF "	DEASSIGN/SYSTEM/EXEC	SSL$COM"
$!
$	WRITE SF "$ IF F$TRNLNM(""SSL$EXE"") .NES."""" THEN -"
$	WRITE SF "	DEASSIGN/SYSTEM/EXEC	SSL$EXE"
$!
$	WRITE SF "$ IF F$TRNLNM(""SSL$INCLUDE"") .NES."""" THEN -"
$	WRITE SF "	DEASSIGN/SYSTEM/EXEC	SSL$INCLUDE"
$!
$	WRITE SF "$ IF F$TRNLNM(""SSL$KEY"") .NES."""" THEN -"
$	WRITE SF "	DEASSIGN/SYSTEM/EXEC	SSL$KEY"
$!
$	WRITE SF "$ IF F$TRNLNM(""SSL$PRIVATE"") .NES."""" THEN -"
$	WRITE SF "	DEASSIGN/SYSTEM/EXEC	SSL$PRIVATE"
$!
$	WRITE SF "$!"
$	WRITE SF "$ IF F$TRNLNM(""OPENSSL"") .NES."""" THEN -"
$	WRITE SF "	DEASSIGN/SYSTEM/EXEC	OPENSSL"
$	WRITE SF "$!"
$	WRITE SF "$ IF F$TRNLNM(""SSL$ROOT"") .NES."""" THEN -"
$	WRITE SF "	DEASSIGN/SYSTEM/EXEC SSL$ROOT"
$	WRITE SF "$!"
$	WRITE SF "$	EXIT"
$	CLOSE SF
$	SET FILE/PROT=WORLD:RE WRK_SSLCOM:SSL$SHUTDOWN.COM
$!
$	COPY SSL$UTILS.COM WRK_SSLCOM:/LOG
$	SET FILE/PROT=WORLD:RE WRK_SSLCOM:SSL$UTILS.COM
$!
$	COPY SSL$SYSTARTUP.COM WRK_SSLCOM:/LOG
$	SET FILE/PROT=WORLD:RE WRK_SSLCOM:SSL$SYSTARTUP.COM
$	COPY SSL$SYSHUTDOWN.COM WRK_SSLCOM:/LOG
$	SET FILE/PROT=WORLD:RE WRK_SSLCOM:SSL$SYSHUTDOWN.COM
$!
$	CERT_DIR := [.CERT_TOOL]
$	CERT_FILES := SSL$AUTH_CERT.COM,SSL$AUTO_CERT.COM,SSL$CERT_TOOL.COM, -
		      SSL$CONF_UTIL.COM,SSL$DRAW_BOX.COM,SSL$EXIT_CMD.TPU, -
		      SSL$FILL_BOX.COM,SSL$HASH_CERT.COM,SSL$HOSTADDR.EXE, -
		      SSL$HOSTNAME.EXE,SSL$INIT_ENV.COM,SSL$INIT_TERM.COM, -
		      SSL$PICK_FILE.COM,SSL$RQST_CERT.COM,SSL$SELF_CERT.COM, -
		      SSL$SHOW_FILE.COM,SSL$SIGN_CERT.COM,SSL$VIEW_CERT.COM, -
		      SSL$REM_ENV.COM
$!
$	I = 0
$ LOOP:
$       CF = F$EDIT(F$ELEMENT(I, ",", CERT_FILES),"TRIM")
$       I = I + 1
$       IF CF .EQS. "," THEN GOTO LOOP_END
$       SET NOON
$       IF F$SEARCH(CERT_DIR+CF) .NES. ""
$       THEN
$         COPY 'CERT_DIR''CF' WRK_SSLCOM:*.*/log
$         SET FILE/PROT=W:RE WRK_SSLCOM:'CF'
$       ENDIF
$       SET ON
$       GOTO LOOP
$ LOOP_END:
$!
$	SHOW SYSTEM/FULL/OUTPUT=WRK_SSLROOT:[PRIVATE]RANDFILE.
$	SET FILE/PROT=WORLD:RE WRK_SSLROOT:[PRIVATE]RANDFILE.
$!
$	COPY SSL010.RELEASE_NOTES WRK_SSLROOT:[000000]/LOG
$	SET FILE/PROT=WORLD:RE WRK_SSLROOT:[000000]SSL010.RELEASE_NOTES
$!
$	EXIT
